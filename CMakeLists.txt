
cmake_minimum_required(VERSION 3.1)
project(moonlight-common-c LANGUAGES C)

set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

option(USE_MBEDTLS "Use MbedTLS instead of OpenSSL" OFF)
option(USE_WERROR "Treat compile warning as errors" ON)
option(BUILD_STATIC "Build static library" OFF)

SET(CMAKE_C_STANDARD 11)

aux_source_directory(src SRC_LIST)
aux_source_directory(enet SRC_LIST)
aux_source_directory(reedsolomon SRC_LIST)

if (BUILD_STATIC)
  add_library(moonlight-common-c STATIC ${SRC_LIST})
else()
  add_library(moonlight-common-c SHARED ${SRC_LIST})
endif()

if(MSVC)
  target_compile_options(moonlight-common-c PRIVATE /W4 /wd4100 /wd4232)
  target_compile_definitions(moonlight-common-c PRIVATE -D_CRT_SECURE_NO_WARNINGS=1 -D_CRT_NONSTDC_NO_DEPRECATE=1)
  target_link_libraries(moonlight-common-c PRIVATE ws2_32.lib qwave.lib winmm.lib)
else()
  target_compile_options(moonlight-common-c PRIVATE -Wall -Wextra -Wno-unused-parameter)
  if (USE_WERROR)
    target_compile_options(moonlight-common-c PRIVATE -Werror)
  endif()
endif()

if (USE_MBEDTLS)
  target_compile_definitions(moonlight-common-c PRIVATE USE_MBEDTLS)
  find_package(MbedTLS QUIET)
  if (MBEDTLS_FOUND)
    target_link_libraries(moonlight-common-c PRIVATE ${MBEDCRYPTO_LIBRARY})
    target_include_directories(moonlight-common-c SYSTEM PRIVATE ${MBEDTLS_INCLUDE_DIRS})
  else()
    # For sub project added via CMake
    target_link_libraries(moonlight-common-c PRIVATE mbedtls)
  endif()
else()
  find_package(OpenSSL 1.0.2 REQUIRED)
  target_link_libraries(moonlight-common-c PRIVATE ${OPENSSL_CRYPTO_LIBRARY})
  target_include_directories(moonlight-common-c SYSTEM PRIVATE ${OPENSSL_INCLUDE_DIR})
endif()

string(TOUPPER "x${CMAKE_BUILD_TYPE}" BUILD_TYPE)
if("${BUILD_TYPE}" STREQUAL "XDEBUG")
  target_compile_definitions(moonlight-common-c PRIVATE LC_DEBUG)
else()
  target_compile_definitions(moonlight-common-c PRIVATE NDEBUG)

  # Avoid false "maybe uninitialized" warning generated by old GCC versions
  # when building with -O2
  if(CMAKE_C_COMPILER_ID STREQUAL "GNU")
    target_compile_options(moonlight-common-c PRIVATE -Wno-maybe-uninitialized)
  endif()
endif()

target_include_directories(moonlight-common-c SYSTEM PUBLIC src)

target_include_directories(moonlight-common-c PRIVATE
  ${CMAKE_CURRENT_SOURCE_DIR}/enet/include
  ${CMAKE_CURRENT_SOURCE_DIR}/reedsolomon
)

target_compile_definitions(moonlight-common-c PRIVATE HAS_SOCKLEN_T)